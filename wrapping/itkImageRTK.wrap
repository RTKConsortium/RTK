itk_wrap_class("itk::Image" POINTER)

  # Wrap ITK unsigned short missing types
  foreach(d 1 2 3 4)
    list(FIND ITK_WRAP_IMAGE_DIMS ${d} _index)
    if (${_index} EQUAL -1 OR NOT ITK_WRAP_unsigned_short)
      itk_wrap_template("US${d}" "${ITKT_US}, ${d}")
    endif()
  endforeach()

  # Wrap ITK unsigned int missing types
  itk_wrap_template("UI2" "${ITKT_UI}, 2")
  itk_wrap_template("UI3" "${ITKT_UI}, 3")

  # Wrap ITK short combination (required by: rtkLookupTableImageFilter)
  if (NOT ITK_WRAP_signed_short)
    itk_wrap_template("SS3" "${ITKT_SS}, 3")
  endif()

  # Wrap ITK dimension 1 missing types
  list(FIND ITK_WRAP_IMAGE_DIMS "1" _index)
  if (${_index} EQUAL -1 OR NOT ITK_WRAP_unsigned_char)
    itk_wrap_template("UC1" "${ITKT_UC}, 1")
  endif()
  if (${_index} EQUAL -1 OR NOT ITK_WRAP_float)
    itk_wrap_template("F1" "${ITKT_F}, 1")
  endif()
  if (${_index} EQUAL -1)
    itk_wrap_template("D1" "${ITKT_D}, 1")
  endif()

  # Wrap ITK dimension 4 missing types
  list(FIND ITK_WRAP_IMAGE_DIMS "4" _index)
  if (${_index} EQUAL -1)
    itk_wrap_template("F4" "${ITKT_F}, 4")
    if (ITK_WRAP_double)
      itk_wrap_template("D4" "${ITKT_D}, 4")
    endif()
  endif()

  # Force ITK_WRAP_IMAGE_DIMS to contain "2;3;4"
  UNIQUE(imageDimensions "${ITK_WRAP_IMAGE_DIMS};2;3;4")
  # Force VECTOR_COMPONENTS to contain "2;3;4;5"
  set(vectorComponents 2 3 4 5)
  foreach(component ${vectorComponents})
    list(FIND ITK_WRAP_VECTOR_COMPONENTS "${component}" _index)
    if (${_index} EQUAL -1)
      foreach(d ${imageDimensions})
        foreach(vt ${WRAP_ITK_VECTOR_REAL})
          itk_wrap_template("${ITKM_${vt}${component}}${d}" "${ITKT_${vt}${component}}, ${d}")
        endforeach()
        foreach(cvt ${WRAP_ITK_COV_VECTOR_REAL})
          itk_wrap_template("${ITKM_${cvt}${component}}${d}" "${ITKT_${cvt}${component}}, ${d}")
        endforeach()
      endforeach()
    endif()
  endforeach()

  # Force ITK_WRAP_VECTOR_COMPONENTS to contain "2;3;4;5"
  UNIQUE(vectorComponents "${ITK_WRAP_VECTOR_COMPONENTS};2;3;4;5")

  # Force ITK_WRAP_IMAGE_DIMS to contain "2;3;4"
  set(imageDimensions 2 3 4)
  foreach(d ${imageDimensions})
    list(FIND ITK_WRAP_IMAGE_DIMS "${d}" _index)
    if (${_index} EQUAL -1)
      foreach(component ${ITK_WRAP_VECTOR_COMPONENTS})
        foreach(vt ${WRAP_ITK_VECTOR_REAL})
          itk_wrap_template("${ITKM_${vt}${component}}${d}" "${ITKT_${vt}${component}}, ${d}")
        endforeach()
        foreach(cvt ${WRAP_ITK_COV_VECTOR_REAL})
          itk_wrap_template("${ITKM_${cvt}${component}}${d}" "${ITKT_${cvt}${component}}, ${d}")
        endforeach()
      endforeach()
    endif()
  endforeach()

  list(FIND ITK_WRAP_VECTOR_COMPONENTS "5" _index)
  if (${_index} EQUAL -1)
    list(FIND WRAP_ITK_COV_VECTOR_REAL "CVD" _index)
    if (${_index} EQUAL -1)
      itk_wrap_template("${ITKM_CVD5}3" "itk::CovariantVector<double,5>, 3")
    endif()
  endif()

itk_end_wrap_class()

itk_wrap_simple_type_swig_interface("itk::Image< float, 2 >::ConstPointer" "itkImageF2_ConstPointer")
itk_wrap_simple_type("itk::Image< float, 2 >::ConstPointer" "itkImageF2_ConstPointer")

itk_wrap_simple_type_swig_interface("itk::Image< float, 3 >::ConstPointer" "itkImageF3_ConstPointer")
itk_wrap_simple_type("itk::Image< float, 3 >::ConstPointer" "itkImageF3_ConstPointer")

itk_wrap_simple_type_swig_interface("itk::Image< float, 4 >::ConstPointer" "itkImageF4_ConstPointer")
itk_wrap_simple_type("itk::Image< float, 4 >::ConstPointer" "itkImageF4_ConstPointer")

#itk_wrap_simple_type_swig_interface("itk::Image< itk::CovariantVector< float, 3 >, 4 >::ConstPointer" "itkImageCVF34_ConstPointer")
#itk_wrap_simple_type("itk::Image< itk::CovariantVector< float, 3 >, 4 >::ConstPointer" "itkImageCVF34_ConstPointer")

if (ITK_WRAP_double)
  itk_wrap_simple_type_swig_interface("itk::Image< double, 3 >::ConstPointer" "itkImageD3_ConstPointer")
  itk_wrap_simple_type("itk::Image< double, 3 >::ConstPointer" "itkImageD3_ConstPointer")

  itk_wrap_simple_type_swig_interface("itk::Image< double, 4 >::ConstPointer" "itkImageD4_ConstPointer")
  itk_wrap_simple_type("itk::Image< double, 4 >::ConstPointer" "itkImageD4_ConstPointer")

  #itk_wrap_simple_type_swig_interface("itk::Image< itk::CovariantVector< double, 3 >, 4 >::ConstPointer" "itkImageCVD34_ConstPointer")
  #itk_wrap_simple_type("itk::Image< itk::CovariantVector< double, 3 >, 4 >::ConstPointer" "itkImageCVD34_ConstPointer")
endif()

set(vectorComponents 2 3 4 5)
foreach(v ${vectorComponents})
  foreach(cvt ${WRAP_ITK_COV_VECTOR_REAL})

    itk_wrap_simple_type_swig_interface("itk::Image< ${ITKT_${cvt}${v}}, 4 >::ConstPointer" "itkImage${ITKM_${cvt}${v}}4_ConstPointer")
    itk_wrap_simple_type("itk::Image< ${ITKT_${cvt}${v}}, 4 >::ConstPointer" "itkImage${ITKM_${cvt}${v}}4_ConstPointer")

  endforeach()
endforeach()
