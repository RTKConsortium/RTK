#-----------------------------------------------------------------------------
# rtk::Functor< int, float >
#-----------------------------------------------------------------------------
set(WRAPPER_AUTO_INCLUDE_HEADERS OFF)
itk_wrap_named_class("rtk::Functor::LUT" "rtkFunctorLUT")

  foreach(i ${WRAP_ITK_INT})
    foreach(t ${WRAP_ITK_REAL})
      itk_wrap_template("${ITKM_${i}}${ITKM_${t}}" "${ITKT_${i}}, ${ITKT_${t}}")
    endforeach()
  endforeach()

  # Wrap ITK unsigned short missing types -- required by LUTbasedVariableI0RawToAttenuationImageFilter
  if (NOT ITK_WRAP_unsigned_short)
    itk_wrap_template("${ITKM_US}${ITKM_F}" "${ITKT_US}, ${ITKT_F}")
  endif()

itk_end_wrap_class()
set(WRAPPER_AUTO_INCLUDE_HEADERS ON)

#-----------------------------------------------------------------------------
# itk::UnaryFunctorImageFilter< itk::Image< int, 3 >, itk::Image< float, 3 >,
#   rtk::Functor::LUT< int, float > >
#-----------------------------------------------------------------------------
itk_wrap_class("itk::UnaryFunctorImageFilter" POINTER)

  foreach(i ${WRAP_ITK_INT})
    foreach(t ${WRAP_ITK_REAL})
      foreach(d ${ITK_WRAP_IMAGE_DIMS})
        itk_wrap_template("I${ITKM_${i}}${d}I${ITKM_${t}}${d}LUT${ITKM_${i}}${ITKM_${t}}"
          "itk::Image<${ITKT_${i}}, ${d}>, itk::Image<${ITKT_${t}}, ${d}>, rtk::Functor::LUT< ${ITKT_${i}}, ${ITKT_${t}} >")
      endforeach()
    endforeach()
  endforeach()

  # Wrap ITK unsigned short missing types -- required by LUTbasedVariableI0RawToAttenuationImageFilter
  if (NOT ITK_WRAP_unsigned_short)
    foreach(d ${ITK_WRAP_IMAGE_DIMS})
      itk_wrap_template("I${ITKM_US}${d}I${ITKM_F}${d}LUT${ITKM_US}${ITKM_F}"
        "itk::Image<${ITKT_US}, ${d}>, itk::Image<${ITKT_F}, ${d}>, rtk::Functor::LUT< ${ITKT_US}, ${ITKT_F} >")
    endforeach()
  endif()
itk_end_wrap_class()

#-----------------------------------------------------------------------------
# rtk::LookupTableImageFilter
#-----------------------------------------------------------------------------
itk_wrap_class("rtk::LookupTableImageFilter" POINTER)

  itk_wrap_image_filter_combinations("${WRAP_ITK_INT}" "${WRAP_ITK_REAL}" 2+)
  
  # Wrap ITK unsigned short missing types -- required by LUTbasedVariableI0RawToAttenuationImageFilter
  if (NOT ITK_WRAP_unsigned_short)
    foreach(d ${ITK_WRAP_IMAGE_DIMS})
      itk_wrap_template("IUS${d}IF${d}" "itk::Image<${ITKT_US}, ${d}>, itk::Image<${ITKT_F}, ${d}>")
    endforeach()
  endif()

itk_end_wrap_class()
